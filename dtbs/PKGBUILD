# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname='sunxi-dtbs'
pkgver=5.16.p1
pkgrel=1
pkgdesc='Additional DTBs for Allwinder-based boards'
conflicts=('orangepi-dtbs' 'nanopi-dtbs')
replaces=('orangepi-dtbs' 'nanopi-dtbs')
_srcname=linux-5.16
arch=('aarch64')
url="http://www.kernel.org/"
license=('GPL2')
makedepends=('bc' 'git')
options=('!strip')

_cfgrel=512f287353f7320125c24ab94bbee2f031ad8542

_patches=(
	'0001-arm-dts-sunxi-OrangePi-Zero-Plus-improve-WiFi-bindin.patch'
	'0002-arm64-dts-sunxi-Orange-Pi-PC2-disable-RTC.patch'
)
source=("http://www.kernel.org/pub/linux/kernel/v5.x/${_srcname}.tar.xz"
	"config-${pkgver}::http://raw.githubusercontent.com/archlinuxarm/PKGBUILDs/${_cfgrel}/core/linux-aarch64/config"
	${_patches[@]})
md5sums=('e6680ce7c989a3efe58b51e3f3f0bf93'
         'e30692ae916c1378fbb7b1845792b2e0'
         '0b570140b2025eebd5878f544ea255df'
         '6db8b898ba6f0172c84a428e47ce623c')

prepare() {
  cd "${srcdir}/${_srcname}"

  for i in ${_patches[@]}; do
    # msg2 "Applying ${i}"
    git apply ../${i}
  done

  cat "${srcdir}/config-${pkgver}" > ./.config

  # add pkgrel to extraversion
  sed -ri "s|^(EXTRAVERSION =)(.*)|\1 \2-${pkgrel}|" Makefile
}

build() {
  cd "${srcdir}/${_srcname}"

  make prepare
  env DTC_FLAGS=--symbols make dtbs
}

package() {
  cd "${srcdir}/${_srcname}"

  local -a _dtbs=(
    'sun50i-h5-nanopi-neo2'
    'sun50i-h5-orangepi-zero-plus'
    'sun50i-h5-orangepi-pc2'
  )
  local _dstdir=usr/share/${pkgname}

  mkdir -p "${pkgdir}/${_dstdir}"

  for i in ${_dtbs[@]}; do
    cp arch/arm64/boot/dts/allwinner/${i}.dtb "${pkgdir}/${_dstdir}"
  done
}
